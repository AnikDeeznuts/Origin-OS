
def "reload"
  // gets currently online
  network "get_online"
  online = network_data
  usernames = {}
  // resets messages
  messages = {}

  // creates a key in messages for all online users
  count = 0
  loop online.len (
    count ++
    c = online.[count]
    usernames.key(c) = c.str
    messages.key(c) = []
  )
endef
current_target = ""

reload
mainloop:
// when new data is recieved
if new_network_data (
  nds = network_data_from
  if network_data.istype("array") (
    usernames.key(nds) = network_data.[1]
    // make sure the messages for this user never exceed 10 total
    messages.key(nds) = messages.key(nds).delete(10)

    // add the new data to the messages for this user
    messages.key(nds) = messages.key(nds) + network_data.str
  )
  new_network_data = false
)

// make a frame and some ui
loc 999 2 0 -20
square window."width" 20 20 : c#111
c #333
frame window."left" window."top" - 40 window."left" + 240 window."bottom" online.len * 20

// Side bar (select a person to send to)
goto
square frame_width - 15 frame_height - 10 40 : c#222
c #fff
count = 0
loop online.len (
  count ++
  loc 2 2 10 count * -20 + scroll_y
  current = online.[count]
  if current_target == current "change_x 10"
  text usernames.key(current) 8
  if clicked "current_target = current"
)
frame "clear"
if current_target == "" (
loc 2 2 250 -60
text "Click a user on the sidebar to start a chat" 9 : c#fff
exit
)

// text input and send data
loc 999 -2 120 20
square window_width - 250 30 10 : c#333
input window_width - 250 25 "send"
loc -2 -2 -20 20
icon "play" 0.7 : c#fff
if clicked or "enter".pressed and can (
  can = false
  send_data = [] + username + input_send
  messages.key(current_target) = messages.key(current_target) + send_data
  network "send" send_data current_target
  input_send = null
)

// render messages
if "enter".pressed.not and mouse_down.not "can = true"
c_msgs = messages.key(current_target)
count = 0
loop c_msgs.len (
  count ++
  loc 2 2 260 -100 - ( count * 20 )
  current = c_msgs.[count]
  if current.[1] == username "loc -2 2 current.[2].len * -10.5 - 20 -100 - ( count * 20 )"
  text current.[2] 10 : c#fff
)

import "win-buttons"
