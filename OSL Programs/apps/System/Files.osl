window "dimensions" 1000 700
window "show"
files_path = "Origin/(C) Users/" ++ username
user_path = files_path ++ "/"
if passed_data != null "Files_path = passed_data"
path_history = [].append(files_path)
history_pos = 1
hidden = []
input_path = Files_path.destr
dta = ""

if config == {} (
  config "> Ui Customisation"
  config "Grid" False
  config "Light Mode" False
  config "> File Sorting"
  config "ascending" False
  config "sort" 2
  config "> Custom Theming"
  config "Use Custom Colours" false
)

def "navigate" "path"
  file "open" path ++ ".folder"
  file "start"
endef

file "open" "Origin/(C) Users/" ++ username ++ ".folder"
file "start"

def "folder" "txt"
  con = hidden.contains(txt)
  foldershow = "up"
  if con (
    foldershow = "down"
  )
  loc 2 2 125 y
  square 225 20 10 0 1
  if clicked and can (
    can = false
    if con.not "hidden = hidden.append(txt)"
    if con "hidden = hidden.delete(txt)"
  )
  if mouse_touching "square 225 20 10 : c#c_seco"
  loc 2 2 230 y
  icon foldershow 0.6 : c#c_icon
  loc 2 2 20 y
  text txt 8
  y -= 35
endef

def "file_folder" "path_input,txt,folder"
  if hidden.contains(folder).not (
    loc 2 2 125 y
    square 225 20 10 0 1
    if mouse_touching "square 225 20 10 1 : c#c_seco"
    if clicked and can "navigate path_input"
    loc 2 2 30 y
    icon icons.key(txt) 0.9
    text txt 8 : c#c_icon chx#30
    y -= 35
  )
  clicked = false
endef

icons = {}
file "open" user_path ++ "Desktop.folder"
icons.key("Desktop") = file.[11]
file "open" user_path ++ "Downloads.folder"
icons.key("Downloads") = file.[11]
file "open" user_path ++ "Documents.folder"
icons.key("Documents") = file.[11]
file "open" user_path ++ "Bin.folder"
icons.key("Bin") = file.[11]
file "open" "Origin/(A) System.folder"
icons.key("System") = file.[11]
file "open" "Origin/(B) Local.folder"
icons.key("Local") = file.[11]
file "open" "Origin/(C) Users.folder"
icons.key("Users") = file.[11]
icons.key(username) = "image" + user_icon.str + "20"

mainloop:
  lightmode = config.key("Light Mode")
  window_colour = #000
  c_icon = #fff
  c_prim = #222
  c_seco = #444
  c_tert = #999
  input_path = null
  w = window_width
  sideshow = w > 700
  loc 999 -2 0 12
  square w 30 10 1 : c#c_prim
  loc -2 2 -30 -70
  square 25 25 10 1
  loc 999 2 120 -70
  if sideshow (
    loc 2 999 125 0
    square 250 window_height 10
    loc 999 2 105 -70
    w -= 330
  )
  if sideshow.not (
    loc 999 2 20 -70
    w -= 160
  )
  square w 25 10
  input w - 10 25 "path" null 10 c_icon
  w = window_width
  colour c_prim
  if sideshow.not (
    loc 2 2 50 -70
    square 65 25 10 1
  )
  loc 2 2 105 -25
  if sideshow.not "loc 2 2 25 -20"
  icon "settings" 0.8 : c#c_icon
  if clicked and can (
    can = false
    window "add" "File-Info"
  )
  change_x 40
  icon "sort" 0.8
  if clicked and can (
    can = false
    config ascending config.key("ascending").destr.not
  )
  loc -2 2 -30 -70
  colour c_icon
  icon "search" 0.8
  if sideshow (
    y = -60
    folder "Favorites"
    file_folder user ++ "Desktop" "Desktop" "Favorites"
    file_folder user ++ "Downloads" "Downloads" "Favorites"
    file_folder user ++ "Documents" "Documents" "Favorites"
    file_folder user ++ "Bin" "Bin" "Favorites"
    folder "Drives"
    file_folder "Origin/(A) System" "System" "Drives"
    file_folder "Origin/(B) Local" "Local" "Drives"
    file_folder "Origin/(C) Users/" ++ username username "Drives"
  )
  if mouse_down.not "can = True"
  import "win-buttons"
