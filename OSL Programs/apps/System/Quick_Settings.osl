// Developer: @Mistium
// Description: This app shows quick info about your device
settings_open = false
can = false
window "front"
window_x = screensize_x / 2 + 128
last_upload = timer + 600
last_synced = 0
syncing = false
error = ""
window "dimensions" 190 300
window "responsive" true
can = false
last_upload = timer + 300
syncing = true
terminal "system uploadfiles"

mainloop:
if new_network_data (
  if network_data_command == "Upload_Files" (
    if network_data.len == 32 (
      last_synced = timestamp
      syncing = false
      error = ""
    ) else (
      error = network_data
    )
    network "My_Files_Usage"
  )
  if network_data_command == "my_files_usage" (
    used = network_data
    used_small = used.[1].divide(1000).round
    used_big = used.[2].divide(1000).round
  )
  network_data_command = null
  new_network_data = false
)

window_target_x = -102
window_x += window_target_x - window_x / 10

window "x" screensize_x / 2 + window_x
window "y" screensize_y / 2 - 160

loc 999 2 0 -25
square 170 30 20 0 1
if bg_redrawn or mouse_touching (
  if clicked and can (
    settings_cha = true
    can = false
  )
  if mouse_down.not (
    if settings_cha (
      if mouse_touching "settings_open = settings_open.not"
      settings_cha = false
      if settings_open.not (
        window "refresh_bg"
      )
    )
  )
  loc 999 2 0 -25
  square 170 30 20 : c#000

  loc -2 2 -25 -25
  c #fff
  if settings_open (
    icon "w 4 line 10 5 0 -5 cont -10 5" 0.7
  ) else (
    icon "w 4 line 10 -5 0 5 cont -10 -5" 0.7
  )
  image user_icon.str 25 : chx#-46
  icon "w 3 line -8 8 -8 -8 line 0 8 0 -8 line 8 8 8 -8 line 6 6 10 6 line 2 -3 -2 -3 line -6 2 -10 2" 0.8 : chx#-46
  if network."connected" "c #fff" else "c #555"
  icon "w 3 cutcircle 0 -6 14 0 50 cutcircle 0 -6 7 0 50 w 4 dot 0 -6" 0.8 : chx#-46
)

loc 999 2 0 -83
square 170 30 20 : c#000
square 30 30 10 : c#171717 chx#-70
if timer > last_upload or clicked and can (
  can = false
  last_upload = timer + 300
  syncing = true
  terminal "system uploadfiles"
)
icon "w 3 cutcircle 0 0 10 -4 65 line 0 15 4.5 9 cont -1 5 cutcircle 0 0 10 14 65 line 0 -15 -4.5 -9 cont 1 -5" 0.7 : c#fff
change 30 -10
if syncing (
  if error == "" (
    text "Syncing" 8
  ) else (
    text error 8
  )
)
if syncing.not (
  if last_synced == 0 (
    text "Not Synced" 8
  ) else (
    text "<last_synced.timestamp(convert-relative)>" 8
  )
)
loc 999 2 -40 -77
if used != "used" (
  text "<used_small>kb / <used_big>kb" 8
)
loc 999 999 0 0
square 170 window_height 20 0 1
if bg_redrawn or mouse_touching and settings_open (
  loc 999 2 0 -203
  square 170 155 20 : c#000
  loc 999 2 0 -237.5
  square 170 35 10 : c#171717
  square 170 80 10 : chy#72.5

  terminal "system display get brightness"
  slider_brightness = return - 10 / 90
  slider 130 0 "brightness" 32 : c#000 chy#20
  terminal "system display set brightness" + ( slider_brightness * 90 + 10 )

  icon "c #333 w 25 dot" 1 : chx#-65
  icon "sun" 0.6 : c#fff
  change 65 -45

  terminal "system sound get volume"
  slider_volume = return / 100
  slider 130 0 "volume" 32 : c#000
  terminal "system sound set volume" + ( slider_volume * 100 )

  icon "c #333 w 25 dot" 1 : chx#-65
  icon "volume-max" 0.7 : c#fff
  loc 2 2 30 -237.5
  terminal "flags donotdisturb get"
  if data "c global_accent" else "c #333"
  square 0 0 25 : hover_size#1.05
  if clicked and can (
    can = false
    terminal "flags donotdisturb set" + data.not
  )
  icon "Moon" 0.7 : c#fff
  date = day + day_number + month.trim(1,3)
  loc 999 2 date.len + 0.5 * -4 -277
  text date 8
)
if mouse_down.not "can = true"
