window "dimensions" 1000 700
window "show"
files_path = "Origin/(C) Users/" ++ username
user_path = files_path ++ "/"
if passed_data != null "Files_path = passed_data"
path_history = [].append(files_path)
history_pos = 1
hidden = []
input_path = Files_path.destr
dta = ""
can = true
folder_button = ""
selected_files = []
file_move_id = 0
file_drag_id = 0

temp = {}
temp."icon" = ["c","#772d1c","square","0","2","10","4","c","#ff724f","square","-8","6","2","2","line","-5","7","3","0","square","0","-1","10","5","w","8","square","0","-0.5","5","0"]
temp."title" = username
temp."url" = user_folder
tabs = []
newtab = {}
newtab."position" = 1
newtab."history" = [].append(temp)
tabs.append(newtab)
current_tab = 1

def "navigate" "path"
  file "open" path ++ ".folder"
  file "start"
endef

if passed_data == null (
  navigate user_folder
) else (
  navigate passed_data
)

def "tab_set" "data1,data2"
  data = tabs.[current_tab]."history"
  data.[data1] = data2
  temp = tabs.[current_tab]
  temp."history" = data
  tabs.[current_tab] = temp
endef

def "folder" "txt"
  con = hidden.contains(txt)
  loc 2 2 125 y
  square 225 20 10 0 1
  if mouse_touching (
    if clicked and can (
      can = false
      if con "hidden.delete(txt)" else "hidden.append(txt)"
    )
    c c_seco
    square 225 20 10
  )
  change_x 100
  c c_icon
  if con (
    icon "down" 0.6
  ) else (
    icon "up" 0.6
  )
  change_x -210
  text txt.str 8
  y -= 35
endef

def "file_folder" "path_input,txt,folder"
  if hidden.contains(folder).not (
    loc 2 2 125 y
    square 225 20 10 0 1
    if mouse_touching (
      c c_seco
      square 225 20 10
      if clicked and can (
        can = false
        navigate path_input
      )
    )
    change_x -95
    icon icons.[txt].str 0.9
    c c_icon : chx#30
    text txt 8
    y -= 35
  )
endef

icons = {}
file "open" user_path ++ "Desktop.folder"
icons."Desktop" = file.[11]
file "open" user_path ++ "Downloads.folder"
icons."Downloads" = file.[11]
file "open" user_path ++ "Documents.folder"
icons."Documents" = file.[11]
file "open" user_path ++ "Bin.folder"
icons."Bin" = file.[11]
file "open" "Origin/(A) System.folder"
icons."System" = file.[11]
file "open" "Origin/(B) Local.folder"
icons."Local" = file.[11]
file "open" "Origin/(C) Users.folder"
icons."Users" = file.[11]
icons.[username] = "image" + user_icon.str + "20"
window_colour = #000
c_icon = #fff
c_prim = #222
c_seco = #444
c_tert = #999

desktop = user_path ++ "Desktop"
downloads = user_path ++ "Downloads"
documents = user_path ++ "Documents"
bin = user_path ++ "Bin"
mainloop:
  if mouse_down.not "can = true"
  input_path = null
  w = window_width
  sideshow = w > 700
  loc 999 -2 0 12
  square w 30 10 1 : c#c_prim
  loc 999 2 120 -70
  if sideshow (
    loc 2 999 125 0
    square 250 window_height 10
    loc 999 2 105 -70
    w -= 330
  ) else (
    loc 999 2 20 -70
    w -= 160
  )
  square w 25 10
  input w - 10 25 "path" null 10 c_icon
  if sideshow.not (
    loc 2 2 50 -70
    square 65 25 10 1 : c#c_prim
  )
  loc -2 2 -30 -70
  c c_prim
  square 25 25 10
  c c_icon
  icon "search" 0.8
  if sideshow (
    y = -60
    folder "Favorites"
    file_folder desktop "Desktop" "Favorites"
    file_folder downloads "Downloads" "Favorites"
    file_folder documents "Documents" "Favorites"
    file_folder Bin "Bin" "Favorites"
    folder "Drives"
    file_folder "Origin/(A) System" "System" "Drives"
    file_folder "Origin/(B) Local" "Local" "Drives"
    file_folder user_folder username "Drives"
  )
  loc 2 2 30 -120
  if sideshow "change 240"
  text "New |" 11 : c#fff
  if clicked and can (
    can = false
    window "add" "New_File" files_path
  )
  change -15 2
  if selected_files.len != 1 (
    files_buttons = []
  ) else (
    file_open = selected_files.[1]
    file "open" file_open "onlyaccess"
    file "get" 13
    if "System" in data (
      files_buttons = ["Open","Copy","Info"]
    ) else (
      files_buttons = ["Open","Edit","Copy","Rename","Bin","Info"]
    )
    if selected_files.len == 1 (
      c #fff
      cur = ""
      count = 0
      loop files_buttons.len (
        count ++
        icon files_buttons.[count] 0.8 : chx#30
        if clicked and can (
          can = false
          cur = files_buttons.[count]
          if cur == "open" (
            file "start"
          )
          if cur == "edit" (
            window "add" "studio" file_open
          )
          if cur == "copy" (
            clipboard "set" file
          )
          if cur == "rename" (
            window "add" "Rename-Helper" file_open
          )
          if cur == "info" (
            window "add" "Files_Info" file_open
          )
          cur = ""
        )
        clicked = false
      )
    )
  )
  if window_width > 1000 and selected_files.len > 0 (
    loc -2 999 -147 -54
    square 270 window_height - 192 10 1 : c#222
    if selected_files.len == 1 (
      loc -2 2 -150 -250
      file "open" file_open "onlyaccess"
      file "render" 5 : c#fff
      loc -2 2 -280 -330 : c#fff
      x = x_position
      file "get" 1
      data1 = data
      file "get" 2
      text data ++ data1 10
      set_x x
      text "File Info" 8.5 : chy#-35
      set_x x
      file "get" 9
      text "Created:" + data.timestamp("convert-relative") 8.5 : chy#-25
      set_x x
      file "get" 10
      text "Last Edited:" + data.timestamp("convert-relative") 8.5 : chy#-25
      set_x x
      file "get" 12
      text "Size:" + data + "Bytes" 8.5 : chy#-25
    )
  )
  file "open" files_path ++ ".folder" "onlyaccess"
  if sideshow "loc 2 -2 265 15" else "loc 2 -2 10 15"
  file "get" 4
  c c_icon
  text "<files.len> items | <selected_files.len> selected" 9
  total_width = window_width - 190
  offset = 20
  if window_width > 700 (
    total_width -= 253
    offset += 253
  )
  positions = tabs.getall("position")
  width = total_width / tabs.len - 15
  width2 = width + 15
  count = 0
  loop tabs.len (
    count ++
    temp = positions.[count]
    cur = tabs.arr[count]."history".arr[temp]
    temp = 10 - offset
    loc 2 2 count - 0.5 * width2 - temp -22.5
    if count == current_tab (
      c c_seco
      square width 25 10
    ) else (
      c c_prim
      square width 25 10
      if clicked "current_tab = count"
    )
    loc 2 2 0 -22.5
    change_x count - 1 * width2 - temp + 20
    c c_icon
    icon cur."icon".str 0.8
    if width > 50 (
      change_x 20
      c c_icon
      text cur."title".str 8 0
    )
    loc 2 2 0 -22.5
    change_x count * width2 - temp - 20
    square 20 20 0 0 1
    if onclick and tabs.len > 1 (
      tabs.delete(count)
      if count > 1 "current_tab --"
    )
    icon "close" 0.5
  )
  if tabs.len < 10 (
    loc -2 2 -160 -22.5
    square 25 25 10 : c#c_seco
    if onclick (
      temp = {}
      temp."icon" = ["c","#772d1c","square","0","2","10","4","c","#ff724f","square","-8","6","2","2","line","-5","7","3","0","square","0","-1","10","5","w","8","square","0","-0.5","5","0"]
      temp."title" = username
      temp."url" = user_folder
      newtab = {}
      newtab."position" = 1
      newtab."history" = [].append(temp)
      tabs.append(newtab)
      current_tab = tabs.len
    )
    icon "Add" 0.55 : c#c_icon
  )
  cur = tabs.[current_tab]
  position = cur."position"
  files_path = cur."history".[position]."url"
  file "open" files_path ++ ".folder" "onlyaccess"
  file "get" 4
  files = data
  if folder_button != "" (
    loc -2 2 -85 -120
    square 140 20 10 : c#c_seco
    if onclick (
      if folder_button == "Empty Bin" (
        each "i" "item" files (
          file "open" item "onlyaccess"
          file "delete"
        )
        files = []
      )
      folder_button = ""
    )
    text folder_button 8 : chx#-65 c#c_icon
  )
  file "close"
  movefileloc files_path
  c c_prim
  if files_temp != files (
    files_temp = files
    folder_button = ""
    if files_path == ( user_folder ++ "/bin" ) (
      if files.len > 0 (
        folder_button = "Empty Bin"
      )
    )
    file "open" files_path ++ ".folder" "onlyaccess"
    file "get" 12
    folder_uuid = data
    file "get" 11
    tab_set "icon" data
    file "get" 4
    files = data
    files_path.to("title")
    each "i" "item" files (
      file "open" item "onlyaccess"
      file "set" 12 file.str.len
      file "get" 2
      if data == "" (
        files.delete(i)
      )
    )
  )
  if window_width > 700 "loc 2 2 260 -140" else "loc 2 2 5 -140"
  temp_x = x_position
  temp_y = y_position
  if window_width > 1000 and selected_files != [] "loc -2 -2 -293 30" else "loc -2 -2 -4 32"
  frame temp_x temp_y x_position y_position files.len
  rightclick ["New Folder Inside","New File"] [].append(folder_uuid)
  total_files = files.len
  if total_files > 0 (
    count = scroll_y // 45
    temp = frame_height / 45
    if count + temp > ( total_files - 1 ) (
      temp = total_files - count
    )
    loop temp (
      count ++
      draw_y = 16 + scroll_y - ( count * 45 )
      loc 999 2 0 draw_y
      current_file = files.[count]
      if selected_files.contains(current_file) (
        c global_accent
        square frame_width - 20 20 15
        c c_prim
        square frame_width - 20 30 10
      ) else (
        c c_prim
        square frame_width - 20 30 15 0 1
      )
      if touching_square and ( mouse_down nor file_dropped == 0 ) (
        file "open" folder_uuid
        file "get" 4
        file "set" 4 data.delete(file_dropped).insert(file_dropped,count)
        file_dropped = 0
      )
      if file_drag_id == 0 (
        rightclick "file" selected_files
        if onclick and file_move_id == 0 (
          if "control".pressed (
            if selected_files.contains(current_file) (
              selected_files.delete(current_file)
            ) else (
              selected_files.append(current_file)
            )
          ) else (
            if "shift".pressed (
              temp = selected_files.last()
              temp = files.index(temp)
              if temp < count (
                temp ++
                selected_files = selected_files ++ files.trim(temp,count)
              ) else (
                temp --
                selected_files = selected_files ++ files.trim(count,temp)
              )
            ) else (
              selected_files = [].append(current_file)
            )
          )
        )
      ) else (
        if file_drag_id == current_file (
          if mouse_down nor mouse_touching (
            file_drag_id = 0
            if mouse_touching.not (
              
            ) else (
            
            )
          )
        )
      )
      loc 2 2 30 draw_y
      file "open" current_file "onlyaccess"
      file "render" 1 : c#c_icon
      file "get" 2
      data2 = data.str
      file "get" 1
      change_x 30
      c c_icon
      file "get" 1
      if data == ".folder" (
        text data2.str 9
        loc -2 2 -300 draw_y
        text "Folder" 9
        loc -2 2 -100 draw_y
        file "get" 4
        text data.len + "Items" 9
      ) else (
        text data2.str ++ data.str 9
        loc -2 2 -300 draw_y
        data = file_types.[data]
        if data == null (
          text "File" 9
        ) else (
          text data.[2].str 9 
        )
      )
    )
  )
  
  frame "clear"
  import "win-buttons"
