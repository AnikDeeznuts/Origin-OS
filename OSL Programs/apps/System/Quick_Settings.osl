// Developer: @Mistium
// Description: This app shows quick info about your computer on your screen
settings_open = false
can = false
window "front"
last_upload = timer + 600
last_synced = 0
syncing = false
error = ""
window "dimensions" 190 300

mainloop:

if new_network_data (
  if network_data_command == "Upload_Files" (
    if network_data.len == 32 (
      last_synced = timestamp
      syncing = false
      error = ""
    ) else (
      error = network_data
    )
    network "My_Files_Usage"
  )
  if network_data_command == "my_files_usage" "used = network_data"
  new_network_data = false
)

window "x" screensize_x / 2 - 102
window "y" screensize_y / 2 - 160

loc 999 2 0 -25
square 170 30 20 0 1
if clicked and can (
  settings_cha = true
  can = false
)
if mouse_down.not (
  if settings_cha (
    if mouse_touching "settings_open = settings_open.not"
    settings_cha = false
  )
  can = true
)
loc 999 2 0 -25
square 170 30 20 : c#000

if settings_open (
  loc 999 2 0 -203
  square 170 155 20
  loc 999 2 0 -260
  square 170 40 10 : c#171717
  square 170 100 10 : chy#85
  terminal "system display get brightness"
  slider_brightness = return - 10 / 90
  slider 130 0 "brightness" 32 : c#000 chy#20
  terminal "system display set brightness" + ( slider_brightness * 90 + 10 )
  icon "c #333 w 25 dot" 1 : chx#-65
  icon "sun" 0.6 : c#fff
  change 65 -45
  slider 130 0 "volume" 32 : c#000
  icon "c #333 w 25 dot" 1 : chx#-65
  icon "volume-max" 0.7 : c#fff
)

loc -2 2 -25 -25
c #fff
if settings_open (
  icon "w 4 line 10 5 0 -5 cont -10 5" 0.7
) else (
  icon "w 4 line 10 -5 0 5 cont -10 -5" 0.7
)
image user_icon.str 25 : chx#-46
icon "w 3 line -8 8 -8 -8 line 0 8 0 -8 line 8 8 8 -8 line 6 6 10 6 line 2 -3 -2 -3 line -6 2 -10 2" 0.8 : chx#-46
if network."connected" "c #fff" else "c #555"
icon "w 3 cutcircle 0 -6 14 0 50 cutcircle 0 -6 7 0 50 w 4 dot 0 -6" 0.8 : chx#-46
loc 999 2 0 -83
square 170 30 20 : c#000
square 30 30 10 : c#171717 chx#-70
if timer > last_upload or clicked and can (
  can = false
  last_upload = timer + 300
  syncing = true
  terminal "system uploadfiles"
)
icon "w 3 cutcircle 0 0 10 -4 65 line 0 15 4.5 9 cont -1 5 cutcircle 0 0 10 14 65 line 0 -15 -4.5 -9 cont 1 -5" 0.7 : c#fff
change 30 -10
if syncing (
  if error == "" (
    text "Syncing" 8
  ) else (
    text error 8
  )
)
if syncing.not (
  if last_synced == 0 (
    text "Not Synced" 8
  ) else (
    text "<last_synced.timestamp(convert-relative)>" 8
  )
)
loc 999 2 -40 -77
if used != "used" (
  text used.[1].divide(100).round / 10 ++ "kb /" + ( used.[2].divide(1000).round ) ++ "kb" 8
)
if "space".pressed "window stop"
